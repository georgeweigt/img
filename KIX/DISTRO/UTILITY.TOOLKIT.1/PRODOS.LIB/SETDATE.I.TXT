FUNCTION SETDATE(MONTH,DAY,YEAR:INTEGER):INTEGER;
VAR RESULT:INTEGER;
BEGIN
#A
 STX _T
 LDY #10        ;YEAR
 LDA (_SP),Y
 BNE SDT8
 DEY
 LDA (_SP),Y
 CMP #86
 BCC SDT8
 CMP #100
 BCS SDT8
 STA _T+1
 LDY #14        ;MONTH
 LDA (_SP),Y
 BNE SDT6
 DEY
 LDA (_SP),Y
 BEQ SDT6
 CMP #13
 BCS SDT6
 STA _T+3
 LDY #12        ;DAY
 LDA (_SP),Y
 BNE SDT7
 DEY
 LDA (_SP),Y
 BEQ SDT7
 STA _T+2
 LDY _T+3
 DEY
 CMP DPM,Y
 BCC SDT1       ;OK
 CPY #1         ;WAS MONTH FEB?
 BNE SDT7
 CMP #29        ;LEAP YEAR TEST?
 BNE SDT7
 LDA _T+1
 AND #3
 BNE SDT7
;
SDT1 EQU *
 LDA _T+1
 ASL
 STA _T+4
 LDA _T+3
 LSR
 LSR
 LSR
 ORA _T+4
 STA $BF91
 LDA _T+3
 ASL
 ASL
 ASL
 ASL
 ASL
 ORA _T+2
 STA $BF90
 LDA #0
 BEQ SDT9       ;ALWAYS
;
DPM EQU *       ;(DAYS PER MONTH)+1 TABLE
 DB 32,29,32,31,32,30,32,32,30,32,31,32
;
SDT6 EQU *      ;INVALID MONTH
 LDA #1
 BNE SDT9
;
SDT7 EQU *      ;INVALID DAY
 LDA #2
 BNE SDT9
;
SDT8 EQU *      ;INVALID YEAR
 LDA #3
;
SDT9 LDY #5
 STA (_SP),Y
 INY
 LDA #0
 STA (_SP),Y
 LDX _T
#
 SETDATE:=RESULT
END;
