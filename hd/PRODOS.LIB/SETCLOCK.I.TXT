FUNCTION SETCLOCK(MONTH,DAY,YEAR,HOURS,MINUTES,DAYOFWEEK:INTEGER):INTEGER;
VAR RESULT:INTEGER;
BEGIN
#A
 STX _T
 LDA CSLOT
 BNE *+5
 JMP STC9
 ORA #$C0       ;PERIPH. ENTRY POINT MSB
 STA STCWRL+1
 LDX #6         ;MAX VALUE FOR PARM [0..X]
 LDY #10        ;MSB OF VALUE ON STACK
 JSR STCHK      ;CHECK EACH PARM (RETURN C=1 IF > MAX)
 BCC STC1
 LDA #6         ;DAY OF WEEK
 BNE STC9
STC1 STA _T+1
 LDX #59
 LDY #12
 JSR STCHK
 BCC STC2
 LDA #5         ;MINUTES
 BNE STC9
STC2 STA _T+2
 LDX #23
 LDY #14
 JSR STCHK
 BCC STC3
 LDA #4         ;HOUR
 BNE STC9
STC3 STA _T+3
 LDX #99        ;YEAR
 LDY #16
 JSR STCHK
 BCS STC3A
 CMP #86
 BCS STC4
STC3A LDA #3
 BNE STC9
STC4 STA _T+4
 LDX #12
 LDY #20
 JSR STCHK
 BCC STC4B
STC4A LDA #1         ;MONTH
 BNE STC9
STC4B CMP #0
 BEQ STC4A
STC5 STA _T+5
 LDY #18
 LDA (_SP),Y
 BNE STC5A
 DEY
 LDA (_SP),Y
 STA _T+6       ;DAY TEMP
 LDY _T+5       ;MONTH 1..12
 DEY
 CMP STCM,Y
 BCC STC6       ;VALID
 CMP #29        ;MAYBE A LEAP YEAR?
 BNE STC5A      ;NO
 CPY #1         ;FEB?
 BNE STC5A      ;NO
 LDA _T+4
 AND #3
 BEQ STC6       ;LEAP YEAR
STC5A LDA #2
STC9 LDY #5
 STA (_SP),Y
 INY
 LDA #0
 STA (_SP),Y
 LDX _T
#
 SETCLOCK := RESULT
END;
#A
STCM DB 32,29,32,31,32,31
     DB 32,32,31,32,31,32
;
STCWRL DW $B
;
STCWR JMP (STCWRL)
;
STC6 EQU *
 LDA #'!+$80
 STA $200
 LDA #$A0
 STA $203
 STA $205
 STA $208
 STA $20B
 STA $20E
 LDA #13+$80
 STA $211
 LDA #'0+$80
 STA $210
 STA $20F
 LDA _T+5       ;MONTH
 LDX #1         ;OFFSET INTO PAGE 2
 JSR STCMK
 LDA _T+6       ;DAY
 LDX #6
 JSR STCMK
 LDA _T+3       ;HOURS
 LDX #9
 JSR STCMK
 LDA _T+2       ;MINUTES
 LDX #$C
 JSR STCMK
 LDA _T+1       ;DAY OF WEEK
 ORA #$B0
 STA $204
 LDX #0
STC7 LDA $200,X
 PHA
 JSR STCWR
 INX
 PLA
 CMP #13+$80
 BNE STC7
 LDA #0         ;NO ERROR
 JMP STC9
;
;
STCX DS 1
;
STCHK EQU *     ;CHECK PARAMETER
 LDA (_SP),Y
 BNE STCHK2
 INX
 STX STCX
 DEY
 LDA (_SP),Y
 CMP STCX
 RTS
STCHK2 SEC
 RTS
;
;
STCTENS DB 0,10,20,30,40,50,60,70,80,90
;
STCMK EQU *     ;CONVERT A TO ASCII DIGITS @ $200,X
 PHA
 LDY #9
STCM2 CMP STCTENS,Y
 BCS STCMK1     ;FOUND TENS PLACE
 DEY
 BNE STCM2
STCMK1 EQU *    ;X=TENS PLACE
 TYA
 ORA #$B0
 STA $200,X
 INX
 SEC
 PLA
 SBC STCTENS,Y
 ORA #$B0
 STA $200,X
 RTS
#
