FUNCTION FORMAT(SLOT,DRIVE:INTEGER; VAR VOLNAME:FILESTRING):INTEGER;
VAR RESULT:INTEGER;
BEGIN
#A
 STX _T
 JSR _MLI
 DB  $C8
 DW  FMTOPLIST
 BEQ *+5
 JMP FMT3
 LDA FMTOPLIST+5
 STA FMTRDLIST+1
 STA FMTCLLIST+1
 JSR _MLI
 DB  $CA
 DW  FMTRDLIST
 PHA
 JSR _MLI
 DB  $CC
 DW FMTCLLIST
 PLA
 BNE FMT3
 LDY #14        ;SLOT MSB
 LDA (_SP),Y
 BNE FMTINV     ;INVALID SLOT/DRIVE RETURNS 'NO DEVICE CONNECTED'
 DEY
 LDA (_SP),Y
 BEQ FMTINV
 CMP #8
 BCS FMTINV
 STA _T+1
 DEY            ;DRIVE MSB
 LDA (_SP),Y
 BNE FMTINV
 DEY
 LDA (_SP),Y
 BEQ FMTINV
 CMP #3
 BCS FMTINV
 STA _T+2       ;DRIVE 1 OR 2
 DEC _T+2       ;NOW 0 OR 1
 ROR _T+2
 ROR _T+2       ;HI BIT SET FOR DRIVE 2
 LDA _T+1
 ASL            ;MAKE BITS 2,1,0
 ASL
 ASL
 ASL            ;INTO BITS 6,5,4
 ORA _T+2
 STA _T+2       ;RESULT IS UNITNUM
;
 LDY #9         ;GET VOLUME NAME NOW
 LDA (_SP),Y
 STA _T+3
 INY
 LDA (_SP),Y
 STA _T+4
 LDY #0
 LDA (_T+3),Y
 CMP #'/
 BNE FMT1
 INY            ;SKIP LEADING / FOR COPY
FMT1 LDA (_T+3),Y
 CMP #32        ;SPACE MARKS END OF NAME
 BEQ FMT2
 JSR FMT8       ;UPPER CASE
 STA $2001,Y
 INY
 CPY #15
 BNE FMT1
FMT2 STY $2000  ;LENGTH
 LDA _T+2       ;UNITNUM
 LDX #0
 LDY #$30       ;BUFFER @ $3000
 JSR $2010
 JMP FMT3
;
FMT8 EQU *
 CMP #$61       ;LOWER CASE A
 BCC FMT8A
 CMP #$7B       ;LOWER CASE Z+1
 BCS FMT8A
 SEC
 SBC #32
FMT8A RTS
;
FMTINV LDA #$28 ;NO DEVICE CONNECTED
FMT3 LDY #5
 STA (_SP),Y    ;SAVE CODE
 INY
 LDA #0
 STA (_SP),Y
 LDX _T
#
 FORMAT := RESULT
END;
#A
FMTOPLIST EQU *
 DB 3
 DW FMTNAME
 DW $3C00       ;$4000-$400
 DB 0
;
FMTRDLIST EQU *
 DB 4,0
 DW $2000
 DW $C3A
 DW 0
;
FMTCLLIST EQU *
 DB 1,0
;
;SET 'FMTNAME' TO THE NAME OF THE BINARY IMAGE OF THE FORMAT.OBJ FILE
;IT CAN BE ANY VALID PATHNAME.
;
FMTNAME STR 'FORMAT.OBJ'
#
