PROCEDURE GETCLOCK(VAR MON,DAY,YEAR,HOUR,MIN,WEEKDAY,SEC,MILLISEC:INTEGER);
BEGIN
#A
 TXA
 PHA
 LDA CSLOT
 BNE *+5
 JMP GTC9
 LDA CSLOT
 ORA #$C0       ;PERIPH. ENTRY POINT MSB
 STA GTCRDL+1
 STA GTCWRL+1
 LDY #5
GTC1 LDA (_SP),Y
 STA _T-5,Y
 INY
 CPY #21
 BNE GTC1
 LDA #'#+$80    ;READ MODE
 JSR GTCWR
 JSR GTCRD
 LDA #0
 LDY #1         ;ZERO ALL MSBS
 STA (_T),Y
 STA (_T+2),Y
 STA (_T+4),Y
 STA (_T+6),Y
 STA (_T+8),Y
 STA (_T+12),Y
 STA (_T+14),Y
 STA GTC6DONE   ;MILLISEC ROUTINE DONE FLAG
 TAX            ;OFFSET INTO PAGE 2
 JSR GTCHEX     ;CONVERT AND STORE MONTH
 STA (_T+14),Y  ;Y ALWAYS RETURNS 0
 JSR GTCHEX     ;DAY OF WEEK
 STA (_T+4),Y
 JSR GTCHEX     ;DAY
 STA (_T+12),Y
 JSR GTCHEX     ;HOUR
 STA (_T+8),Y
 JSR GTCHEX     ;MINUTE
 STA (_T+6),Y
 JSR GTCHEX     ;SECOND
 STA (_T+2),Y
;
 LDA $3FE
 PHA
 LDA $3FF
 PHA
 LDA #>GTC6     ;INT'PT ADDRESS
 STA $3FE
 LDA #<GTC6
 STA $3FF
 LDA CSLOT
 ASL
 ASL
 ASL
 ASL
 TAY
 LDA #$40
 STA $C080,Y
 CLI
GTC4 BIT GTC6DONE
 BPL GTC4
 PLA
 STA $3FF
 PLA
 STA $3FE
;
GTC2 JSR _MLI
 DB  $82       ;GET_TIME
 DW  0
 LDY #1
 STA (_T+10),Y ;YEAR MSB
 DEY
 LDA $BF91
 LSR
 STA (_T+10),Y
;
GTC9 EQU *
 PLA
 TAX
#
END;
#A
GTC6DONE DS 1   ;INT'RPT EXECUTED FLAG
GTCY   DS 1
GTCR   DS 1
GTCRDL DW 8
GTCWRL DW $B
GTCRD JMP (GTCRDL)
GTCWR JMP (GTCWRL)
;
GTCHEX EQU *    ;INDEX INTO $200 IN X-REG
 LDY #0
 STY GTCR
 LDA $200,X     ;TENS PLACE
 AND #15
 BEQ GTCH2
 TAY
GTCH1 CLC
 LDA GTCR       ;RESULT SO FAR
 ADC #10
 STA GTCR
 DEY
 BNE GTCH1
GTCH2 INX
 LDA $200,X     ;ONES PLACE
 AND #15
 CLC
 ADC GTCR       ;RESULT IN A
 INX            ;SKIP COMMA
 INX
 LDY #0
 RTS
;
GTC6 EQU *      ;GET MILLISEC VIA INTERRUPT
 LDA CSLOT
 ASL
 ASL
 ASL
 ASL
 TAY
 LDA $C088,Y
 LDA $C080,Y
 LDA #0
 LDY CSLOT
 STA $5F8,Y
 JSR GTCRD
;
 LDX #$10
 JSR GTCHEX
 STA (_T),Y
 LDA $20F
 AND #15
 BEQ GTC6A
 TAX
GTC6B CLC
 LDY #0
 LDA (_T),Y
 ADC #100
 STA (_T),Y
 INY
 LDA (_T),Y
 ADC #0
 STA (_T),Y
 DEX
 BNE GTC6B
GTC6A EQU *
 LDA #$80
 STA GTC6DONE
 LDA $45
 RTI
#
