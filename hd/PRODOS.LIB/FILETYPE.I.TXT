FUNCTION FILETYPE(VAR PATHNAME:PATHSTRING; VAR FTYPE:INTEGER):INTEGER;
VAR RESULT:INTEGER;
BEGIN
#A
 STX _T
 LDY #9
 LDA (_SP),Y       ;ADDRESS OF FTYPE
 STA _T+4
 INY
 LDA (_SP),Y
 STA _T+5
 LDY #11
 JSR MAKEPATH
 LDA _T+2
 STA FTLIST+1
 LDA _T+3
 STA FTLIST+2
 JSR _MLI
 DB  $C4      ;GET_FILE_INFO
 DW  FTLIST
 BNE FTYPE9
 TAY
 INY
 STA (_T+4),Y   ;MSB IS ZERO
 DEY
 LDA FTLIST+4
 STA (_T+4),Y
 TYA
FTYPE9 LDY #5
 STA (_SP),Y
 INY
 LDA #0
 STA (_SP),Y
 LDY #11
 JSR RESTPATH
 LDX _T
#
 FILETYPE := RESULT
END;
#A
FTLIST DB 10
 DS 17
 IFNDEF MAKEPATH
MAKEPATH EQU *
 LDA (_SP),Y    ;ADDRESS OF STRING ADDRESS
 STA _T+2
 INY
 LDA (_SP),Y
 STA _T+3
 LDY #64
MPTH1 LDA (_T+2),Y
 CMP #$20       ;SPACE?
 BNE MPTH2      ;NO. FOUND LAST CHAR OF PATHNAME
 DEY
 BPL MPTH1
 INY            ;Y=0
 TYA
 STA (_T+2),Y   ;MAKE LENGTH ZERO
 RTS
MPTH2 INY       ;PATH LENGTH
 STY PLENX
 DEY
MPTH3 LDA (_T+2),Y
 INY
 STA (_T+2),Y    ;MOVE STRING FORWARD ONE BYTE
 DEY
 DEY
 BPL MPTH3
 INY            ;Y=0
 LDA PLENX
 STA (_T+2),Y
 RTS
PLENX DS 1
 ENDIF
;
 IFNDEF RESTPATH
RESTPATH EQU *  ;RESTORE PATH @(_T+2)
 LDY #0
 LDA (_T+2),Y   ;LENGTH
 STA PLENX
 INY
RPTH1 LDA (_T+2),Y
 DEY
 STA (_T+2),Y   ;MOVE STRING BACK ONE BYTE
 INY
 INY
 DEC PLENX
 BPL RPTH1
 RTS
 ENDIF
#
