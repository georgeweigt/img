FUNCTION DELETE(VAR PATHNAME:PATHSTRING):INTEGER;
VAR RESULT:INTEGER;
BEGIN
#A
 STX _T
 LDY #9
 JSR MAKEPATH   ;CONVERT STRING TO PATH
 LDA #1
 STA _T+1       ;PARM COUNT
 JSR _MLI
 DB $C1         ;DESTROY
 DW _T+1
 LDY #5
 STA (_SP),Y    ;MLI ERROR CODE
 LDA #0
 INY
 STA (_SP),Y    ;MSB IS ALWAYS ZERO
 JSR RESTPATH   ;RESTORE PATHNAME TO STRING
 LDX _T
#
 DELETE := RESULT
END;
#A
 IFNDEF MAKEPATH
MAKEPATH EQU *
 LDA (_SP),Y    ;ADDRESS OF STRING ADDRESS
 STA _T+2
 INY
 LDA (_SP),Y
 STA _T+3
 LDY #64
MPTH1 LDA (_T+2),Y
 CMP #$20       ;SPACE?
 BNE MPTH2      ;NO. FOUND LAST CHAR OF PATHNAME
 DEY
 BPL MPTH1
 INY            ;Y=0
 TYA
 STA (_T+2),Y   ;MAKE LENGTH ZERO
 RTS
MPTH2 INY       ;PATH LENGTH
 STY PLENX
 DEY
MPTH3 LDA (_T+2),Y
 INY
 STA (_T+2),Y    ;MOVE STRING FORWARD ONE BYTE
 DEY
 DEY
 BPL MPTH3
 INY            ;Y=0
 LDA PLENX
 STA (_T+2),Y
 RTS
PLENX DS 1
 ENDIF
;
 IFNDEF RESTPATH
RESTPATH EQU *  ;RESTORE PATH @(_T+2)
 LDY #0
 LDA (_T+2),Y   ;LENGTH
 STA PLENX
 INY
RPTH1 LDA (_T+2),Y
 DEY
 STA (_T+2),Y   ;MOVE STRING BACK ONE BYTE
 INY
 INY
 DEC PLENX
 BPL RPTH1
 RTS
 ENDIF
#
