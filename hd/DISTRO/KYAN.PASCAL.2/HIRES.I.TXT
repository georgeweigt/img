(* CHANGE TO GRAPHICS MODE *)

PROCEDURE HGR;
BEGIN
#A
 LDA #>$2000
 STA _T
 LDA #<$2000
 STA _T+1
 LDA #32
 STA _T+2
 LDA #0
 TAY
HGR1 STA (_T),Y
 INY
 BNE HGR1
 INC _T+1
 DEC _T+2
 BNE HGR1
 LDA $C053
 LDA $C054
 LDA $C057
 LDA $C050
#
END;

(* CHANGE TO TEXT MODE *)
PROCEDURE TX;
BEGIN
#A
 LDA $C054
 LDA $C051
#
END;

(*
0=WHITE (TV)
1=GREEN
2=VIOLET
3=RED
4=BLUE
5=BLACK (TV)

6=WHITE (MONITOR)
7=BLACK (MONITOR)
*)

PROCEDURE PLOT(X,Y,COLOR:INTEGER);
BEGIN
#A
 JSR PLOT
#
END;
#A
PLOT LDY #5+2
 LDA (_SP),Y
 CMP #>192
 INY
 LDA (_SP),Y
 SBC #<192
 BCS PLOT3
 INY
 LDA (_SP),Y
 CMP #>280
 INY
 LDA (_SP),Y
 SBC #<280
 BCS PLOT3
 LDY #5+2
 LDA (_SP),Y
 AND #$F8
 LSR
 LSR
 TAY
 LDA PLOT10,Y
 STA _T
 INY
 LDA PLOT10,Y
 STA _T+1
 LDY #5+2
 LDA (_SP),Y
 AND #7
 ASL
 ASL
 ADC _T+1
 STA _T+1
 LDY #7+2
 LDA (_SP),Y
 STA _T+2
 INY
 LDA (_SP),Y
 LDY #8
PLOT1 ASL _T+2
 ROL
 CMP #7
 BCC PLOT2
 SBC #7
 INC _T+2
PLOT2 DEY
 BNE PLOT1
 TAY
 LDA PLOT11,Y
 STA _T+3
 JSR COLOR
PLOT3 EQU *
 RTS
#

#A
COLOR LDY #3+2
 LDA (_SP),Y
 BNE COLR10
 LDA _T+3 ;BLACK
 EOR #$FF
 LDY _T+2
 AND (_T),Y
 STA (_T),Y
 ASL _T+3
 BPL COLR01
 LDA #1
 STA _T+3
 INY
 CPY #40
 BEQ COLR02
COLR01 LDA _T+3
 EOR #$FF
 AND (_T),Y
 STA (_T),Y
COLR02 RTS
COLR10 CMP #1 ;GREEN
 BNE COLR20
 LDY #7+2
 LDA (_SP),Y
 AND #1
 BEQ COLR11
 LDY _T+2
 LDA (_T),Y
 AND #$7F
 ORA _T+3
 STA (_T),Y
COLR11 RTS
COLR20 CMP #2 ;VIOLET
 BNE COLR30
 LDY #7+2
 LDA (_SP),Y
 AND #1
 BNE COLR21
 LDY _T+2
 LDA (_T),Y
 AND #$7F
 ORA _T+3
 STA (_T),Y
COLR21 RTS
COLR30 CMP #3 ;RED
 BNE COLR40
 LDY #7+2
 LDA (_SP),Y
 AND #1
 BEQ COLR31
 LDY _T+2
 LDA (_T),Y
 ORA #$80
 ORA _T+3
 STA (_T),Y
COLR31 RTS
COLR40 CMP #4 ;BLUE
 BNE COLR50
 LDY #7+2
 LDA (_SP),Y
 AND #1
 BNE COLR41
 LDY _T+2
 LDA (_T),Y
 ORA #$80
 ORA _T+3
 STA (_T),Y
COLR41 RTS
COLR50 CMP #5 ;WHITE
 BNE COLR60
 LDY _T+2
 LDA (_T),Y
 ORA _T+3
 STA (_T),Y
 ASL _T+3
 BPL COLR51
 LDA #1
 STA _T+3
 INY
 CPY #40
 BEQ COLR52
COLR51 LDA (_T),Y
 ORA _T+3
 STA (_T),Y
COLR52 RTS
COLR60 CMP #6
 BNE COLR70
 LDA _T+3
 LDY _T+2
 ORA (_T),Y
 STA (_T),Y
 RTS
COLR70 CMP #7
 BNE COLR80
 LDA _T+3
 EOR #$FF
 LDY _T+2
 AND (_T),Y
 STA (_T),Y
 RTS
COLR80 RTS
PLOT10 DW 8192
 DW 8320
 DW 8448
 DW 8576
 DW 8704
 DW 8832
 DW 8960
 DW 9088
 DW 8232
 DW 8360
 DW 8488
 DW 8616
 DW 8744
 DW 8872
 DW 9000
 DW 9128
 DW 8272
 DW 8400
 DW 8528
 DW 8656
 DW 8784
 DW 8912
 DW 9040
 DW 9168
PLOT11 DB 1
 DB 2
 DB 4
 DB 8
 DB 16
 DB 32
 DB 64
#

PROCEDURE DRAW(X1,Y1,X2,Y2,COLOR:INTEGER);
BEGIN
#A
J EQU 20
I EQU 22
DX EQU 24
DY EQU 26
XDIR EQU 28
YDIR EQU 30
 SEC
 LDY #7+2
 LDA (_SP),Y
 LDY #11+2
 SBC (_SP),Y
 STA DX
 LDY #8+2
 LDA (_SP),Y
 LDY #12+2
 SBC (_SP),Y
 STA DX+1
;
 SEC
 LDY #5+2
 LDA (_SP),Y
 LDY #9+2
 SBC (_SP),Y
 STA DY
 LDY #6+2
 LDA (_SP),Y
 LDY #10+2
 SBC (_SP),Y
 STA DY+1
;
 LDA #>1
 STA XDIR
 LDA #<1
 STA XDIR+1
 LDA DX+1
 BPL DRAW1
 SEC
 LDA #0
 SBC DX
 STA DX
 LDA #0
 SBC DX+1
 STA DX+1
 LDA #>-1
 STA XDIR
 LDA #<-1
 STA XDIR+1
;
DRAW1 LDA #>1
 STA YDIR
 LDA #<1
 STA YDIR+1
 LDA DY+1
 BPL DRAW2
 SEC
 LDA #0
 SBC DY
 STA DY
 LDA #0
 SBC DY+1
 STA DY+1
 LDA #>-1
 STA YDIR
 LDA #<-1
 STA YDIR+1
;
DRAW2 LDY #11+2
 LDA (_SP),Y
 LDY #7+2
 STA (_SP),Y
 LDY #12+2
 LDA (_SP),Y
 LDY #8+2
 STA (_SP),Y
 LDY #9+2
 LDA (_SP),Y
 LDY #5+2
 STA (_SP),Y
 LDY #10+2
 LDA (_SP),Y
 LDY #6+2
 STA (_SP),Y
;
 LDA DX
 CMP DY
 LDA DX+1
 SBC DY+1
 BCC DRAW3
;
 LDA DX+1
 STA I+1
 LSR
 STA J+1
 LDA DX
 STA I
 ROR
 STA J
;
DRAW4 LDA J
 CMP DX
 LDA J+1
 SBC DX+1
 BCC DRAW5
;
 SEC
 LDA J
 SBC DX
 STA J
 LDA J+1
 SBC DX+1
 STA J+1
;
 CLC
 LDY #5+2
 LDA (_SP),Y
 ADC YDIR
 STA (_SP),Y
 LDY #6+2
 LDA (_SP),Y
 ADC YDIR+1
 STA (_SP),Y
;
DRAW5 CLC
 LDA J
 ADC DY
 STA J
 LDA J+1
 ADC DY+1
 STA J+1
;
 JSR PLOT
;
 CLC
 LDY #7+2
 LDA (_SP),Y
 ADC XDIR
 STA (_SP),Y
 LDY #8+2
 LDA (_SP),Y
 ADC XDIR+1
 STA (_SP),Y
;
 CLC
 LDA I
 ADC #>-1
 STA I
 LDA I+1
 ADC #<-1
 STA I+1
 BPL DRAW4
DRAWEX EQU *
#
END;
#A
DRAW3 LDA DY+1
 STA I+1
 LSR
 STA J+1
 LDA DY
 STA I
 ROR
 STA J
;
DRAW6 LDA J
 CMP DY
 LDA J+1
 SBC DY+1
 BCC DRAW7
;
 SEC
 LDA J
 SBC DY
 STA J
 LDA J+1
 SBC DY+1
 STA J+1
;
 CLC
 LDY #7+2
 LDA (_SP),Y
 ADC XDIR
 STA (_SP),Y
 LDY #8+2
 LDA (_SP),Y
 ADC XDIR+1
 STA (_SP),Y
;
DRAW7 CLC
 LDA J
 ADC DX
 STA J
 LDA J+1
 ADC DX+1
 STA J+1
;
 JSR PLOT
;
 CLC
 LDY #5+2
 LDA (_SP),Y
 ADC YDIR
 STA (_SP),Y
 LDY #6+2
 LDA (_SP),Y
 ADC YDIR+1
 STA (_SP),Y
;
 CLC
 LDA I
 ADC #>-1
 STA I
 LDA I+1
 ADC #<-1
 STA I+1
 BPL DRAW6
 BMI DRAWEX
#
