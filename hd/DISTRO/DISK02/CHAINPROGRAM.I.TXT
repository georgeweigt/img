PROCEDURE CHAINPROGRAM(VAR PATHNAME:PATHSTRING);
BEGIN
#A
   LDY #5
   JSR MAKEPATH
   STX CHPINFO+1
   STX CHPOPEN+1
   STY CHPINFO+2
   STY CHPOPEN+2
   JSR _MLI
   DB  $C4
   DW  CHPINFO
   LDA CHPINFO+4
   CMP #$FF
   BNE CHP1
   LDX #>$2000
   LDY #<$2000
   JMP CHP1A
CHP1 EQU *
   LDX CHPINFO+5
   LDY CHPINFO+6
CHP1A EQU *
   STX $BEFE
   STY $BEFF
CHP2 EQU *
   JSR _MLI
   DB  $C8
   DW  CHPOPEN
   LDA CHPOPEN+5
   STA $BEFD
   LDX #0
CHP3 EQU *
   LDA CHREL,X
   STA $BE00,X
   INX
   CPX #200
   BNE CHP3
   JMP $BE00
;
CHREL EQU *
RELCON EQU $BE00-*
   LDA $BEFD
   STA CHPREAD+1
   LDA $BEFE
   STA CHPREAD+2
   LDA $BEFF
   STA CHPREAD+3
   JSR _MLI
   DB  $CA
   DW  CHPREAD
   LDA #1
   STA CHPREAD
   JSR _MLI
   DB  $CC
   DW  CHPREAD
   JMP ($BEFE)
;
CHPREAD EQU *+RELCON
   DB 4
   DB 0
   DW 0
   DW $FFFF
   DW 0
;
CHPINFO EQU *
   DB 10
   DS 18
;
CHPOPEN EQU *
   DB 3
   DW 0
   DW $9000
   DB 0
;
MAKEPATH EQU *
   LDA (_SP),Y
   STA _T+2
   INY
   LDA (_SP),Y
   STA _T+3
   LDY #64
MPATH1 EQU *
   LDA (_T+2),Y
   CMP #32
   BNE MPATH2
   DEY
   BPL MPATH1
   INY
   TYA
   STA (_T+2),Y
   JMP MPATH3
MPATH2 EQU *
   INY
   STY _T
   DEY
MPATH4 EQU *
   LDA (_T+2),Y
   INY
   STA (_T+2),Y
   DEY
   DEY
   BPL MPATH4
   INY
   LDA _T
   STA (_T+2),Y
MPATH3 EQU *
   LDX _T+2
   LDY _T+3
   RTS
#
END;
