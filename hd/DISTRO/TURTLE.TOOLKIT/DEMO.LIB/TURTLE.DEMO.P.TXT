#A
_USESHIRES
#
PROGRAM TURTLE_DEMO;

(* TURTLE GRAPHICS DEMONSTRATION PROGRAM
   FOR USE WITH TURTLEGRAPHICS TOOLKIT

   COPYRIGHT (C) 1986 BY
   KYAN SOFTWARE, INC.  *)

TYPE
#I TURTLE.TYPES.I

VAR
   VALIDCOLORS:SET OF COLORTYPE;
   COLORINUSE : COLORTYPE;


#I INITTURTLE.I
#I PENCOLOR.I
#I TURTLEX.I
#I TURTLEY.I
#I TURTLEANG.I
#I VIEWPORT.I
#I FULLPORT.I
#I TURN.I
#I TURNTO.I
#I MOVETO.I
#I MOVE.I
#I FILLPORT.I
#I FILLAREA.I
#I TEXTMODE.I


PROCEDURE NEXTCOLOR;
BEGIN
   REPEAT
      COLORINUSE:=SUCC(COLORINUSE);
      IF COLORINUSE=NONE THEN COLORINUSE:=GREEN
   UNTIL COLORINUSE IN VALIDCOLORS
END;

PROCEDURE SWIRL;
CONST DISTANCE=160;
VAR J:INTEGER;
BEGIN
   INITTURTLE;
   MOVETO(TURTLEX-(DISTANCE DIV 2), TURTLEY-(DISTANCE DIV 2));
   PENCOLOR(WHITE1);
   FOR J:=1 TO DISTANCE DO
   BEGIN
      MOVE(DISTANCE-J);
      TURN(-89);
   END;
   READLN
END;

PROCEDURE CRISSCROSS;
VAR I:INTEGER;
BEGIN
   INITTURTLE;
   COLORINUSE:=GREEN;
   FOR I:=45 DOWNTO 0 DO
   BEGIN
      TURNTO(215); PENCOLOR(NONE); MOVETO(12*I,0);
      PENCOLOR(COLORINUSE); MOVE(340);
      PENCOLOR(NONE);
      MOVETO(280-I*12,0); TURNTO(315);
      PENCOLOR(COLORINUSE); MOVE(340);
      NEXTCOLOR
   END;
   READLN
END;

PROCEDURE PATTERNS;
CONST DX=56; DY=48;
VAR I,J,K:INTEGER;
BEGIN
   INITTURTLE; MOVETO(0,0); PENCOLOR(WHITE1);
   FOR I:=1 TO 4 DO
   BEGIN
      MOVE(191); IF (I MOD 2)=1 THEN MOVE(88);
      TURN(-90)
   END;
   FOR I:=1 TO 4 DO
   BEGIN
      PENCOLOR(NONE); MOVETO(I*DX,0);
      TURNTO(270); PENCOLOR(WHITE1); MOVE(191);
      PENCOLOR(NONE); MOVETO(0,I*DY);
      TURNTO(0);   PENCOLOR(WHITE1); MOVE(279)
   END;
   I:=DX+2; J:=2;
   FOR K:=3 TO 21 DO
   BEGIN
      FILLAREA(I,J,K); I:=I+DX+2;
      IF (I > 279) THEN BEGIN I:=2; J:=J+DY+2 END
   END;
   READLN
END;

PROCEDURE POLYGON(SIDES:INTEGER);
VAR I,ANGLEINC:INTEGER;
BEGIN
   TURNTO(0); PENCOLOR(WHITE1);
   ANGLEINC:=(360 DIV SIDES)-90;
   IF (360 MOD SIDES)=0 THEN
      FOR I:= 1 TO SIDES DO
      BEGIN
         TURN(ANGLEINC+90);
         MOVE(79);
      END
END;

PROCEDURE SHAPES;
VAR K:INTEGER;
BEGIN
   INITTURTLE; MOVETO(179,191);
   K:=8;
   REPEAT
      POLYGON(K);
      PENCOLOR(NONE); TURNTO(90);
      MOVE(K*4);
      K:=K-1;
      IF K=7 THEN K:=6
   UNTIL K=4;
   READLN
END;

PROCEDURE PICTURE;
CONST D=80;
VAR I:INTEGER;
BEGIN
   INITTURTLE; TURNTO(180); MOVE(D);
   TURNTO(90); MOVE(20);
   PENCOLOR(WHITE1); TURNTO(0);
   FOR I:=1 TO 4 DO
   BEGIN
      MOVE(D); TURN(-90)
   END;
   FILLAREA(TURTLEX+2,TURTLEY+2,17);
   TURNTO(30); MOVE(D); TURNTO(0); MOVE(D);
   TURNTO(210); MOVE(D);
   FILLAREA(TURTLEX-1,TURTLEY-1,12);
   TURNTO(270); MOVE(D); TURNTO(30); MOVE(D);
   TURNTO(90); MOVE(D);
   FILLAREA(TURTLEX-1,TURTLEY+1,14);
   FILLAREA(140,72+D,14);
   READLN
END;

BEGIN
   VALIDCOLORS:=[GREEN,RED,WHITE,BLUE,VIOLET,BLACK];
   SWIRL;
   CRISSCROSS;
   PATTERNS;
   SHAPES;
   PICTURE;
   TEXTMODE
END.
