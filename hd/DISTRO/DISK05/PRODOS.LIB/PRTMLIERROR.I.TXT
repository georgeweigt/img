PROCEDURE PRTMLIERROR(ERRCODE:INTEGER);
BEGIN
#A
 STX _T
 LDY #5
 LDA (_SP),Y    ;ERROR CODE IN A
 LDY #30        ;# OF POSSIBLE ERRORS
PME1 CMP PMET,Y
 BEQ PME3       ;FOUND A MATCH
 DEY
 BNE PME1
 PHA            ;Y=0 MEANS UNDEFINED ERROR CODE
 LDX MTI 
 LDY MTI+1
 JSR PME6       ;PRINT STRING @ (X,Y)
 PLA
 JSR $FDDA      ;PRINT ERROR IN HEX
 JMP PME9       ;AND EXIT
PME3 TYA
 ASL
 TAY            ;INDEX INTO ASCII TABLE
 LDA MTI,Y
 TAX
 INY
 LDA MTI,Y
 TAY
 JSR PME6       ;PRINT THE MESSAGE
PME9 LDX _T
#
END;
#A
PME6 STX _T+1
 STY _T+2
 LDY #0
PME7 LDA (_T+1),Y
 BEQ PME8
 ORA #$80
 JSR $FDED
 INY
 BNE PME7
PME8 RTS
;
;LIST OF ERROR CODES:
;
PMET DB $FF     ;ANY UNDEFINED CODE
 DB 0,1,4,$25,$27
 DB $28,$2B,$2E,$40,$42
 DB $43,$44,$45,$46,$47
 DB $48,$49,$4A,$4B,$4C
 DB $4E,$50,$51
 DB $52,$53,$55,$56
 DB $57,$5A,$4D
;
;TEXT OF ERRORS-ADDRESSES FOR LOOK-UP
;
MTI DW MTX0,MTX1,MTX2
 DW MTX3,MTX4,MTX5
 DW MTX6,MTX7,MTX8
 DW MTX9,MTX10,MTX11
 DW MTX12,MTX13,MTX14
 DW MTX15,MTX16,MTX17
 DW MTX18,MTX19,MTX20
 DW MTX21,MTX22,MTX23
 DW MTX24,MTX25,MTX26
 DW MTX27,MTX28,MTX29
 DW MTX30
;
;ERROR MESSAGES:
;
MTX0 ASC 'PRODOS ERROR $'
 DB 0
MTX1 ASC 'NO ERROR'
 DB 0
MTX2 ASC 'BAD FUNCTION CODE'
 DB 0
MTX3 ASC 'BAD PARM COUNT'
 DB 0
MTX4 ASC 'INTERRUPT TABLE FULL'
 DB 0
MTX5 ASC 'I/O ERROR'
 DB 0
MTX6 ASC 'NO DEVICE CONNECTED'
 DB 0
MTX7 ASC 'DISK WRITE PROTECTED'
 DB 0
MTX8 ASC 'DISK SWITCHED'
 DB 0
MTX9 ASC 'INVALID PATHNAME'
 DB 0
MTX10 ASC 'TOO MANY OPEN FILES'
 DB 0
MTX11 ASC 'BAD REFERENCE NUMBER'
 DB 0
MTX12 ASC 'DIRECTORY NOT FOUND'
 DB 0
MTX13 ASC 'VOLUME NOT FOUND'
 DB 0
MTX14 ASC 'FILE NOT FOUND'
 DB 0
MTX15 ASC 'DUPLICATE FILENAME'
 DB 0
MTX16 ASC 'VOLUME FULL'
 DB 0
MTX17 ASC 'VOLUME DIRECTORY FULL'
 DB 0
MTX18 ASC 'VERSION NOT SAME'
 DB 0
MTX19 ASC 'UNSUPPORTED STORAGE TYPE'
 DB 0
MTX20 ASC 'END OF FILE'
 DB 0
MTX21 ASC 'FILE ACCESS ERROR'
 DB 0
MTX22 ASC 'REOPEN NOT ALLOWED'
 DB 0
MTX23 ASC 'DIRECTORY IS DAMAGED'
 DB 0
MTX24 ASC 'NOT A PRODOS VOLUME'
 DB 0
MTX25 ASC 'PARM OUT OF RANGE'
 DB 0
MTX26 ASC 'TOO MANY VOLUMES'
 DB 0
MTX27 ASC 'BAD BUFFER ADDRESS'
 DB 0
MTX28 ASC 'DUPLICATE VOLUME NAMES'
 DB 0
MTX29 ASC 'VOLUME DAMAGED'
 DB 0
MTX30 ASC 'CANNOT MOVE PAST EOF'
 DB 0
#
