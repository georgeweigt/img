PROCEDURE IDMACHINE(VAR VERSION:CHAR; VAR CARD80,EXTEND80:BOOLEAN);
BEGIN
#A
 STX _T
 LDY #5
IDM1 LDA (_SP),Y
 STA _T-4,Y
 INY
 CPY #11
 BNE IDM1
 LDY #0
 LDA #$20       ;SPACE
 LDX $FBB3
 CPX #$38       ;APPLE ][?
 BEQ IDM2
 LDA #'+ 
 CPX #$EA       ;APPLE ][+ (OR ///)?
 BEQ IDM2
 LDA #'c        ;LOWER CASE C (FOR //C)
 LDX $FBC0
 BEQ IDM2
 LDA #'E        ;OLD (UNENHANCED) APPLE //E
 CPX #$EA
 BEQ IDM2
 LDA #'e        ;LOWER CASE E (MUST BE //E WITH 65C02)
IDM2 STA (_T+5),Y
 LDA $BF98
 PHA
 AND #2
 BEQ IDM3       ;TAKE IF NO CARD
 LDA #1
IDM3 STA (_T+3),Y
 LDX #1
 PLA
 AND #$30
 CMP #$30       ;128K?
 BEQ IDM4       ;YES
 DEX
IDM4 TXA
 STA (_T+1),Y
 LDX _T
#
END;
