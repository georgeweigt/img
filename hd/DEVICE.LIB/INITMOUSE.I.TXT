PROCEDURE INITMOUSE;
VAR MOUSESLOT:INTEGER;
BEGIN
   MOUSESLOT := FINDMOUSE;
#A
 STX _T
 LDY #5         ;LSB OF SLOT NUMBER
 LDA (_SP),Y
 STA MSLOT
 BEQ IM9        ;GET OUT IF NO MOUSE
;
 LDY #$19       ;OFFSET TO INITMOUSE INDEX
 JSR MADR       ;CALC JUMP ADDRESS
 JSR MCALL      ;CONDITION X,Y AND PERFORM INDIRECT JUMP
;
 LDY #$12
 JSR MADR       ;SETMOUSE
 LDA #1         ;PASSIVE MODE
 JSR MCALL
;
 LDY #$15       ;CLEARMOUSE
 JSR MADR
 JSR MCALL
;
 JMP IM9        ;EXIT TO PASCAL
;
;----------------------------
;
MSLOT DB 0
MLOC  DW 0 ;FOR INDIRECT JUMPS
;
MADR EQU *      ;CALCULATE ADDRESS OF MOUSE ROUTINE
 LDA #$C0
 ORA MSLOT
 STA MLOC+1
 STA _T+14
 LDA #0
 STA _T+13
 LDA (_T+13),Y   ;OFFSET INTO INDEX TABLE
 STA MLOC
 RTS
;
MCALL EQU *     ;CALL ROUTINE @(MLOC). ENTER WITH A CONDITIONED
 PHA
 LDX MLOC+1     ;X IS CN
 LDA MSLOT
 ASL
 ASL
 ASL
 ASL
 TAY            ;Y IS N0
 PLA
 JMP (MLOC)
;
IM9 LDX _T
#
END;
