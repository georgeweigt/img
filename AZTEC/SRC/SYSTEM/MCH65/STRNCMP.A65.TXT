*:ts=8
*
* Copyright (C) 1983,84,85 by Manx Software Systems, Inc.
*
	instxt	<zpage.h>
*
*	int
*	strncmp(s1, s2, n)
*	  char *s1, *s2;
*	  int n;
*
*	compare at most n chars of str1 with str2
*

S1	equ	R0+2
S2	equ	R0+4
N	equ	R0+6

	public	strncmp_
strncmp_
	ldy	#7
load	lda	(SP),Y		;copy args to registers
	sta	R0,Y
	dey
	bne	load
	ldx	N
	beq	decN
	bne	skip
mainlp	dex			;main loop
	bne	ndec
decN	dec	N+1
	bmi	equal		;end of N, must be equal
ndec	iny
	bne	skip
	inc	S1+1
	inc	S2+1
skip	lda	(S1),Y		;get next char from s1
	bne	compare		;non-zero, go compare it
	lda	(S2),Y		;check if both zero
	bne	less		;no, must be less

equal	ldy	#0		;equal case
	beq	done
less	ldy	#-1		;return -1
done	sty	R0
	sty	R0+1
	rts

compare	cmp	(S2),Y		;compare to other string
	beq	mainlp		;same, continue
	bcc	less		;less, return it
	ldy	#0		;else must be greater
	sty	R0+1
	iny			;return 1
	sty	R0
	rts

